#version 460
#extension GL_NV_ray_tracing : require

layout(binding = 0, set = 0) uniform accelerationStructureNV topLevelAS;
layout(binding = 1, set = 0, rgba8) uniform readonly image2D diffuseImage;
layout(binding = 2, set = 0, rgba8) uniform readonly image2D specularImage;
layout(binding = 3, set = 0, rgba32f) uniform readonly image2D normalImage;
layout(binding = 4, set = 0, rgba32f) uniform readonly image2D otherImage;
layout(binding = 5, set = 0, rgba8) uniform image2D outImage;
layout(binding = 6, set = 0) uniform CameraProperties 
{
	mat4 view;
    mat4 proj;
    mat4 viewInv;
    mat4 projInv;
} cam;

layout (push_constant) uniform pcBlock {
	vec3 lightPosition;
	float power;
} pcb;

layout(location = 0) rayPayloadNV uint hit;

const float pi = 3.141592653589793;

float ggxDist(const vec3 h, const vec3 n, float alpha) {
    float cosine_sq = dot(h, n);
    cosine_sq *= cosine_sq;
    float tan_sq = 1.0f / cosine_sq - 1.0f;
    float alpha_sq = alpha * alpha;

    tan_sq += alpha_sq;
    tan_sq *= tan_sq;
    float D = alpha_sq / (pi * cosine_sq * cosine_sq * tan_sq);

    return D;
}

float ggxG1(const vec3 v, const vec3 h, const vec3 n, float alpha) {
    float cosv_sq = dot(v, n);
    cosv_sq *= cosv_sq;
    float tanv_sq = 1.0f / cosv_sq - 1.0f;

    if (tanv_sq <= 1e-15)
        return 1.0f;
    else if (dot(h, v) <= 1e-15)
        return 0.0f;

    float alpha_sq = alpha * alpha;

    tanv_sq *= alpha_sq;
    tanv_sq += 1.0f;
    tanv_sq = 1.0f + sqrt(tanv_sq);

    return 2.0f/tanv_sq;
}

float ggxBrdf(const vec3 wo, const vec3 wi, const vec3 n, float alpha) {
    vec3 h = normalize(wo + wi);
    
    float D = ggxDist(h, n, alpha);
    float G = ggxG1(wo, h, n, alpha) * ggxG1(wi, h, n, alpha);

    return (D * G) / (4.0f * dot(wo, n));
}

void main() 
{	
	const ivec2 pixel = ivec2(gl_LaunchIDNV.xy);
	const vec4 diffuseColor = imageLoad(diffuseImage, pixel).rgba;
	const vec4 other = imageLoad(otherImage, pixel).rgba;

	vec4 color = vec4(0, 0, 0, 1);
	if (other.w > 3.5 && other.w < 4.5)
		color = diffuseColor;

	else {
		const vec4 specularColor = imageLoad(specularImage, pixel).rgba;
		const vec4 normal = imageLoad(normalImage, pixel).rgba;

		const vec2 pixelCenter = vec2(gl_LaunchIDNV.xy) + vec2(0.5);
		const vec2 inUV = pixelCenter/vec2(gl_LaunchSizeNV.xy);
		vec2 d = inUV * 2.0 - 1.0;

		vec4 origin = cam.viewInv * vec4(0,0,0,1);
		vec4 target = cam.projInv * vec4(d.x, d.y, 1, 1) ;
		vec4 viewDir = cam.viewInv * vec4(normalize(target.xyz), 0) ;
	
		// for shadow rays
		origin = origin + (other.x - 0.05f) * viewDir;
		vec4 lightPosition = vec4(pcb.lightPosition, 0);
		float distance = length(lightPosition.xyz - origin.xyz);
		vec4 lightDir = (lightPosition - origin) / distance;
	
		float cos = dot(lightDir.xyz, normal.xyz);	
	
		if (cos > 0) {
			hit = 0;
			traceNV(topLevelAS, gl_RayFlagsOpaqueNV, gl_RayFlagsTerminateOnFirstHitNV|gl_RayFlagsOpaqueNV|gl_RayFlagsSkipClosestHitShaderNV
				, 0, 0, 0, origin.xyz, 0.01f, lightDir.xyz, 10000.0f, 0);

			vec4 specularColor = other.w > 0.5f ? specularColor * ggxBrdf(-viewDir.xyz, lightDir.xyz, normal.xyz, normal.w) : vec4(0, 0, 0, 1);
			vec4 diffuseColor = diffuseColor * cos / pi;
		
			color = pcb.power * (diffuseColor + specularColor) * hit  / (distance * distance);
		}
	}

	imageStore(outImage, pixel, color);
	
}
